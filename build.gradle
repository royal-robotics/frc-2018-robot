

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.11"
    id "com.moowork.node" version "1.2.0"
}

repositories {
    mavenCentral()
    jcenter()
}

def TEAM = 2522
def ROBOT_CLASS = "frc.team2522.robot.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.

deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
       artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
           targets << "roborio"
       }
    }
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile pathfinder()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()

    testCompile 'junit:junit:4.12'
    compile 'junit:junit:4.12'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}


task runMotionProfile(type: JavaExec) {
    classpath = files('build/libs/Motion-Profile.jar')
    main = 'MotionProfile.Generate'
}

task viewMotionProfile(type: Exec, dependsOn: runMotionProfile) {
    commandLine 'cmd', '/c', 'Rscript', 'scripts/motion-profile.R'
}

task generateMotionProfile(type: NodeTask) {
  script = file( 'scripts/generateMotionProfile/motionProfile.js')
  execOverrides {
    it.workingDir = 'scripts/generateMotionProfile'
  }
}